// root build.gradle
plugins {
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

allprojects {
    group = 'com.tp'
    version = '1.0-SNAPSHOT'

    repositories { mavenCentral() }

    // 모든 모듈 공통: 자바 툴체인 + 소스 JAR
    apply plugin: 'java'
    java {
        toolchain { languageVersion = JavaLanguageVersion.of(21) }
    }

    // Lombok은 전모듈 공통(테스트 포함)
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
        testCompileOnly 'org.projectlombok:lombok:1.18.30'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    }

    test { useJUnitPlatform() }
}

project(':common') {
    apply plugin: 'java-library'
    java {
        withSourcesJar()
    }
    dependencies {
        testImplementation platform("org.junit:junit-bom:5.10.2")
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }
}

configure(subprojects.findAll { it.name in ['domain', 'notification', 'app-api', 'app-batch'] }) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
    if (name in ['app-api', 'app-batch']) {
        tasks.named('bootJar').configure { enabled = true }
        tasks.named('jar').configure { enabled = false }
    } else {
        tasks.named('bootJar').configure { enabled = false }
        tasks.named('jar').configure { enabled = true }
    }
}
