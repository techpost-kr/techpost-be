// root build.gradle
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.hyeyeoung'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    // 공통으로 사용되는 의존성
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'


        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }
}

// 라이브러리 모듈
configure(subprojects.findAll { it.name in ['common', 'domain', 'notification'] }) {
    bootJar { enabled = false }
    jar { enabled = true }
}

// 실행 가능한 애플리케이션
configure(subprojects.findAll { it.name in ['app-api', 'app-batch'] }) {
    bootJar { enabled = true }
    jar { enabled = false }
}
