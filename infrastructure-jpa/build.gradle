plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
}

ext {
    querydslVersion = '5.1.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    implementation project(':common')
    implementation project(':domain')
    implementation project(':application')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Querydsl
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
}

// QueryDSL Q클래스 생성 위치 지정
def querydslDir = "${layout.buildDirectory.get()}/generated/querydsl"

// QueryDSL Q클래스 소스 위치 지정
sourceSets {
    main.java.srcDir querydslDir
}

// clean 시에 QClass 디렉토리 삭제
clean {
    delete file(querydslDir)
}

// compile 시에 사용할 옵션 지정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}


